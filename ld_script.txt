ENTRY(Start)

gNumMusicPlayers = 8;
gMaxLines = 0;

SECTIONS {
    . = 0x2000000;

    ewram (NOLOAD) :
    ALIGN(4)
    {
        ewram_start = .;
        INCLUDE "sym_ewram.ld"
        src/agb_flash.o(.bss);
        *libgcc.a:fp-bit.o(.bss);
        *libgcc.a:dp-bit.o(.bss);
        *libc.a:syscalls.o(.bss);
        . = ALIGN(16);
        INCLUDE "sym_ewram2.ld"
        *libc.a:impure.o(.data);
        *libc.a:locale.o(.data);
        *libc.a:mallocr.o(.data);
        gUnknown_203BC04 = .;
        . = 0x40000;
    }

    . = 0x3000000;

    iwram (NOLOAD) :
    ALIGN(4)
    {
        iwram_start = .;
        INCLUDE "sym_iwram.ld"
        . = 0x8000;
    }

    . = 0x8000000;

    .text :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        asm/code.o(.text);
        src/code_80001E0.o(.text);
        asm/arm_func.o(.text);
        asm/code_2.o(.text);
        src/code.o(.text);
        asm/event_flag.o(.text);
        src/event_flag.o(.text);
        asm/code_8002774.o(.text);
        src/memory.o(.text);
        asm/memory.o(.text);
        src/memory1.o(.text);
        asm/code_80035F0.o(.text);
        src/other_random.o(.text);
        src/util.o(.text);
        src/bg_palette_buffer.o(.text);
        src/input.o(.text);
        asm/input.o(.text);
        asm/code_8004AA0.o(.text);
        src/sprite.o(.text);
        asm/sprite.o(.text);
        asm/code_800558C.o(.text);
        src/code_800558C.o(.text);
        src/random.o(.text);
        src/text.o(.text);
		. = ALIGN(4);
        asm/text.o(.text);
        asm/code_8009804.o(.text);
        src/file_system.o(.text);
        src/main.o(.text);
        asm/code_800B540.o(.text);
        src/code_800B5F0.o(.text);
        asm/code_800B5F0.o(.text);
        src/music_pre.o(.text);
        asm/music.o(.text);
        src/music.o(.text);
        src/code_800C9CC.o(.text);
        asm/code_800C9CC.o(.text);
        src/flash.o(.text);
        src/code_800D090.o(.text);
        asm/code_800D090.o(.text);
        src/code_80118A4.o(.text);
        asm/code_80118A4.o(.text);
        src/code_80118A4_1.o(.text);
        asm/fatal_system.o(.text);
        src/fatal_system.o(.text);
        asm/fatal_system_1.o(.text);
        src/save.o(.text);
        asm/save.o(.text);
        src/save_mid.o(.text);
        asm/save1.o(.text);
        src/save1.o(.text);
        asm/code_8012A18.o(.text);
        src/code_801D760.o(.text);
        asm/code_801D760.o(.text);
        src/code_8027C84_pre.o(.text);
        asm/code_8027C84.o(.text);
        src/wonder_mail.o(.text);
        asm/wonder_mail.o(.text);
        asm/personality_test.o(.text);
        src/personality_test.o(.text);
        asm/code_803D110.o(.text);
        src/code_8040094.o(.text);
        asm/code_8040094.o(.text);
        src/code_8041AD0.o(.text);
        asm/code_8041AD0.o(.text);
        src/code_80428A0.o(.text);
        asm/code_80428A0.o(.text);
        src/code_8048480.o(.text);
        asm/code_8048480.o(.text);
        src/code_80521D0.o(.text);
        asm/code_80521D0.o(.text);
        src/pokemon.o(.text);
        asm/pokemon.o(.text);
        src/pokemon_mid.o(.text);
        asm/pokemon_1.o(.text);
        src/pokemon_1.o(.text);
        src/code_808DAB4.o(.text);
        asm/code_808DAB4.o(.text);
        src/code_808EAB0.o(.text);
        asm/code_808EAB0.o(.text);
        src/dungeon.o(.text);
        asm/code_809017C.o(.text);
        src/code_8092334.o(.text);
        asm/code_8092334.o(.text);
        src/code_8094F88.o(.text);
        asm/code_8095014.o(.text);
        src/code_809747C.o(.text);
        asm/code_809747C.o(.text);
        src/code_8097F40.o(.text);
        asm/code_8097F40.o(.text);
        asm/code_8098468.o(.text);
        src/code_8098BDC.o(.text);
        asm/code_8098BDC.o(.text);
        src/code_809D148.o(.text);
        asm/code_809D148.o(.text);
        src/code_80A26CC.o(.text);
        asm/code_80A26CC.o(.text);
        src/code_80A7714.o(.text);
        asm/code_80A7714.o(.text);
        asm/m4a_1.o(.text);
        src/m4a.o(.text);
        *libagbsyscall.a:CpuSet.o(.text);
        *libagbsyscall.a:SoundBiasReset.o(.text);
        *libagbsyscall.a:SoundBiasSet.o(.text);
        *libagbsyscall.a:VBlankIntrWait.o(.text);
        src/agb_flash.o(.text);
        src/agb_flash_1m.o(.text);
        src/agb_flash_mx.o(.text);
        src/agb_flash_le.o(.text);
        *libgcc.a:_call_via_rX.o(.text);
        *libgcc.a:_divsi3.o(.text);
        *libgcc.a:_dvmd_tls.o(.text);
        *libgcc.a:_fixunssfsi.o(.text);
        *libgcc.a:_modsi3.o(.text);
        *libgcc.a:_umodsi3.o(.text);
        *libgcc.a:fp-bit.o(.text);
        *libgcc.a:_muldi3.o(.text);
        *libgcc.a:dp-bit.o(.text);
        *libgcc.a:_lshrdi3.o(.text);
        *libgcc.a:_negdi2.o(.text);
        *libc.a:memcpy.o(.text);
        *libc.a:memset.o(.text);
        *libc.a:sprintf.o(.text);
        *libc.a:strcat.o(.text);
        *libc.a:strcmp.o(.text);
        *libc.a:strcpy.o(.text);
        *libc.a:strlen.o(.text);
        *libc.a:strncpy.o(.text);
        *libc.a:vfprintf.o(.text);
        *libc.a:vsprintf.o(.text);
        *libc.a:wsetup.o(.text);
        *libc.a:dtoa.o(.text);
        *libc.a:fflush.o(.text);
        *libc.a:findfp.o(.text);
        *libc.a:freer.o(.text);
        *libc.a:fvwrite.o(.text);
        *libc.a:fwalk.o(.text);
        *libc.a:locale.o(.text);
        *libc.a:makebuf.o(.text);
        *libc.a:mallocr.o(.text);
        *libc.a:mbtowc_r.o(.text);
        *libc.a:memchr.o(.text);
        *libc.a:memmove.o(.text);
        *libc.a:mlock.o(.text);
        *libc.a:mprec.o(.text);
        *libc.a:s_isinf.o(.text);
        *libc.a:s_isnan.o(.text);
        *libc.a:sbrkr.o(.text);
        *libc.a:stdio.o(.text);
        *libc.a:syscalls.o(.text);
        *libc.a:writer.o(.text);
        *libc.a:callocr.o(.text);
        *libc.a:closer.o(.text);
        *libc.a:errno.o(.text);
        *libc.a:fstatr.o(.text);
        *libc.a:abort.o(.text);
        *libc.a:libcfunc.o(.text);
        *libc.a:lseekr.o(.text);
        *libc.a:readr.o(.text);
        *libgcc.a:_udivsi3.o(.text);
    } =0

    .rodata :
    ALIGN(4)
    {
        data/data.o(.rodata);
        src/file_system.o(.rodata);
        data/data_80B9BB8.o(.rodata);
        data/data_80EBA18.o(.rodata);
        data/personality_test.o(.rodata);
        src/personality_test.o(.rodata);
        data/data_80F4278.o(.rodata);
        src/m4a_tables.o(.rodata);
        src/agb_flash.o(.rodata);
        src/agb_flash_1m.o(.rodata);
        src/agb_flash_mx.o(.rodata);
        src/agb_flash_le.o(.rodata);
        *libc.a:sprintf.o(.rodata);
        *libc.a:vfprintf.o(.rodata);
        *libc.a:dtoa.o(.rodata);
        *libc.a:impure.o(.rodata);
        *libc.a:locale.o(.rodata);
        *libc.a:mprec.o(.rodata);
        *libc.a:syscalls.o(.rodata);
        . = ALIGN(4);
        data/data_8270000.o(.rodata);
    } =0

    unk_code_section :
    ALIGN(4)
    {
        unk_code = .;
        asm/code_8272724.o(.rodata);
        src/code_8272724.o(.text);
        asm/code_8272724.o(.text);
        unk_code_end = .;
    }

    unk_code_section_size = (unk_code_end - unk_code);
    unk_code_ram_end = unk_code_ram + unk_code_section_size;
    end = unk_code_ram_end;

    . = 0x8300000;
    unk_data :
    ALIGN(4)
    {
        data/unk_data.o(.rodata);
    }

    . = 0x8300500;
    system_sbin_section :
    ALIGN(4)
    {
        data/system_sbin.o(.rodata);
    }

    . = 0x8380000;
    titlemenu_sbin_section :
    ALIGN(4)
    {
        data/titlemenu_sbin.o(.rodata);
    }

    . = 0x83B0000;
    dungeon_sbin_section :
    ALIGN(4)
    {
        data/dungeon_sbin.o(.rodata);
    }

    . = 0x8510000;
    monster_sbin_section :
    ALIGN(4)
    {
        data/monster_sbin.o(.rodata);
    }

    . = 0x9740000;
    effect_sbin_section :
    ALIGN(4)
    {
        data/effect_sbin.o(.rodata);
    }

    . = 0x9890000;
    ground_sbin_section :
    ALIGN(4)
    {
        data/ground_sbin.o(.rodata);
    }

    . = 0x9E60000;
    ornament_sbin_section :
    ALIGN(4)
    {
        data/ornament_sbin.o(.rodata);
    }

    sound_data :
    ALIGN(4)
    {
        data/sound_data.o(.rodata);
    }

    /* DWARF 2 sections */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    .debug_info     0 : { *(.debug_info) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
