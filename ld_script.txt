ENTRY(Start)

gNumMusicPlayers = 8;
gMaxLines = 0;

SECTIONS {
    . = 0x2000000;

    ewram (NOLOAD) :
    ALIGN(4)
    {
        ewram_start = .;
        INCLUDE "sym_ewram.ld"
        src/agb_flash.o(.bss);
        tools/agbcc/lib/libgcc.a:fp-bit.o(.bss);
        tools/agbcc/lib/libgcc.a:dp-bit.o(.bss);
        tools/agbcc/lib/libc.a:syscalls.o(.bss);
        . = ALIGN(16);
        INCLUDE "sym_ewram2.ld"
        tools/agbcc/lib/libc.a:impure.o(.data);
        tools/agbcc/lib/libc.a:locale.o(.data);
        tools/agbcc/lib/libc.a:mallocr.o(.data);
        gUnknown_203BC04 = .;
        . = 0x40000;
    }

    . = 0x3000000;

    iwram (NOLOAD) :
    ALIGN(4)
    {
        iwram_start = .;
        INCLUDE "sym_iwram.ld"
        . = 0x8000;
    }

    . = 0x8000000;

    .text :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        asm/code.o(.text);
        asm/event_flag.o(.text);
        asm/code_8002774.o(.text);
        src/memory.o(.text);
        asm/memory.o(.text);
        asm/code_80035F0.o(.text);
        src/other_random.o(.text);
        src/util.o(.text);
        src/bg_palette_buffer.o(.text);
        asm/input.o(.text);
        asm/code_8004AA0.o(.text);
        src/sprite.o(.text);
        asm/sprite.o(.text);
        asm/code_800558C.o(.text);
        src/random.o(.text);
        src/text.o(.text);
		. = ALIGN(4);
        asm/text.o(.text);
        asm/code_8009804.o(.text);
        src/file_system.o(.text);
        asm/code_800AAA0.o(.text);
        src/main.o(.text);
        asm/code_800B540.o(.text);
        src/flash.o(.text);
        asm/code_800D090.o(.text);
        src/save.o(.text);
        asm/save.o(.text);
        src/save1.o(.text);
        asm/code_8012A18.o(.text);
        asm/m4a_1.o(.text);
        src/m4a_2.o(.text);
        asm/m4a_3.o(.text);
        src/m4a_4.o(.text);
        asm/syscall.o(.text);
        src/agb_flash.o(.text);
        src/agb_flash_1m.o(.text);
        src/agb_flash_mx.o(.text);
        src/agb_flash_le.o(.text);
        tools/agbcc/lib/libgcc.a:_call_via_rX.o(.text);
        tools/agbcc/lib/libgcc.a:_divsi3.o(.text);
        tools/agbcc/lib/libgcc.a:_dvmd_tls.o(.text);
        tools/agbcc/lib/libgcc.a:_fixunssfsi.o(.text);
        tools/agbcc/lib/libgcc.a:_modsi3.o(.text);
        tools/agbcc/lib/libgcc.a:_umodsi3.o(.text);
        tools/agbcc/lib/libgcc.a:fp-bit.o(.text);
        tools/agbcc/lib/libgcc.a:_muldi3.o(.text);
        tools/agbcc/lib/libgcc.a:dp-bit.o(.text);
        tools/agbcc/lib/libgcc.a:_lshrdi3.o(.text);
        tools/agbcc/lib/libgcc.a:_negdi2.o(.text);
        tools/agbcc/lib/libc.a:memcpy.o(.text);
        tools/agbcc/lib/libc.a:memset.o(.text);
        tools/agbcc/lib/libc.a:sprintf.o(.text);
        tools/agbcc/lib/libc.a:strcat.o(.text);
        tools/agbcc/lib/libc.a:strcmp.o(.text);
        tools/agbcc/lib/libc.a:strcpy.o(.text);
        tools/agbcc/lib/libc.a:strlen.o(.text);
        tools/agbcc/lib/libc.a:strncpy.o(.text);
        tools/agbcc/lib/libc.a:vfprintf.o(.text);
        tools/agbcc/lib/libc.a:vsprintf.o(.text);
        tools/agbcc/lib/libc.a:wsetup.o(.text);
        tools/agbcc/lib/libc.a:dtoa.o(.text);
        tools/agbcc/lib/libc.a:fflush.o(.text);
        tools/agbcc/lib/libc.a:findfp.o(.text);
        tools/agbcc/lib/libc.a:freer.o(.text);
        tools/agbcc/lib/libc.a:fvwrite.o(.text);
        tools/agbcc/lib/libc.a:fwalk.o(.text);
        tools/agbcc/lib/libc.a:locale.o(.text);
        tools/agbcc/lib/libc.a:makebuf.o(.text);
        tools/agbcc/lib/libc.a:mallocr.o(.text);
        tools/agbcc/lib/libc.a:mbtowc_r.o(.text);
        tools/agbcc/lib/libc.a:memchr.o(.text);
        tools/agbcc/lib/libc.a:memmove.o(.text);
        tools/agbcc/lib/libc.a:mlock.o(.text);
        tools/agbcc/lib/libc.a:mprec.o(.text);
        tools/agbcc/lib/libc.a:s_isinf.o(.text);
        tools/agbcc/lib/libc.a:s_isnan.o(.text);
        tools/agbcc/lib/libc.a:sbrkr.o(.text);
        tools/agbcc/lib/libc.a:stdio.o(.text);
        tools/agbcc/lib/libc.a:syscalls.o(.text);
        tools/agbcc/lib/libc.a:writer.o(.text);
        tools/agbcc/lib/libc.a:callocr.o(.text);
        tools/agbcc/lib/libc.a:closer.o(.text);
        tools/agbcc/lib/libc.a:errno.o(.text);
        tools/agbcc/lib/libc.a:fstatr.o(.text);
        tools/agbcc/lib/libc.a:abort.o(.text);
        tools/agbcc/lib/libc.a:libcfunc.o(.text);
        tools/agbcc/lib/libc.a:lseekr.o(.text);
        tools/agbcc/lib/libc.a:readr.o(.text);
        tools/agbcc/lib/libgcc.a:_udivsi3.o(.text);
    } =0

    .rodata :
    ALIGN(4)
    {
        data/data.o(.rodata);
        src/file_system.o(.rodata);
        data/data_80B9BB8.o(.rodata);
        src/m4a_tables.o(.rodata);
        src/agb_flash.o(.rodata);
        src/agb_flash_1m.o(.rodata);
        src/agb_flash_mx.o(.rodata);
        src/agb_flash_le.o(.rodata);
        tools/agbcc/lib/libc.a:sprintf.o(.rodata);
        tools/agbcc/lib/libc.a:vfprintf.o(.rodata);
        tools/agbcc/lib/libc.a:dtoa.o(.rodata);
        tools/agbcc/lib/libc.a:impure.o(.rodata);
        tools/agbcc/lib/libc.a:locale.o(.rodata);
        tools/agbcc/lib/libc.a:mprec.o(.rodata);
        tools/agbcc/lib/libc.a:syscalls.o(.rodata);
        . = ALIGN(4);
        data/data_8270000.o(.rodata);
    } =0

    unk_code_section :
    ALIGN(4)
    {
        unk_code = .;
        asm/code_8272724.o(.rodata);
        asm/code_8272724.o(.text);
        unk_code_end = .;
    }

    unk_code_section_size = (unk_code_end - unk_code);
    unk_code_ram_end = unk_code_ram + unk_code_section_size;
    end = unk_code_ram_end;

    . = 0x8300000;
    unk_data :
    ALIGN(4)
    {
        data/unk_data.o(.rodata);
    }

    . = 0x8300500;
    system_sbin_section :
    ALIGN(4)
    {
        data/system_sbin.o(.rodata);
    }

    . = 0x8380000;
    titlemenu_sbin_section :
    ALIGN(4)
    {
        data/titlemenu_sbin.o(.rodata);
    }

    . = 0x83B0000;
    dungeon_sbin_section :
    ALIGN(4)
    {
        data/dungeon_sbin.o(.rodata);
    }

    . = 0x8510000;
    monster_sbin_section :
    ALIGN(4)
    {
        data/monster_sbin.o(.rodata);
    }

    . = 0x9740000;
    effect_sbin_section :
    ALIGN(4)
    {
        data/effect_sbin.o(.rodata);
    }

    . = 0x9890000;
    ground_sbin_section :
    ALIGN(4)
    {
        data/ground_sbin.o(.rodata);
    }

    . = 0x9E60000;
    ornament_sbin_section :
    ALIGN(4)
    {
        data/ornament_sbin.o(.rodata);
    }

    sound_data :
    ALIGN(4)
    {
        data/sound_data.o(.rodata);
    }

    /* DWARF 2 sections */
    .debug_aranges  0 : { *(.debug_aranges) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    .debug_info     0 : { *(.debug_info) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }

    /* Discard everything not specifically mentioned above. */
    /DISCARD/ :
    {
        *(*);
    }
}
